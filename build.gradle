import java.nio.file.Paths

buildscript {
    ext {
        springBootVersion = '2.2.2.RELEASE'
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'java'
    id "org.asciidoctor.convert" version "1.5.3"
    id "org.sonarqube" version "2.7.1"
    id 'jacoco'
    id "nebula.lint" version "16.7.0"

}


apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

group = 'com.kokozzang'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = '11'
description = 'spring sample project'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

gradleLint.rules = ['dependency-parentheses']

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compile'org.springframework.boot:spring-boot-starter-aop'

    compile 'com.google.guava:guava:27.1-jre'
    compile group: 'org.modelmapper', name: 'modelmapper', version: '2.3.2'
    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'  exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'

    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

ext {
    snippetsDir = file("$buildDir/generated-snippets")
    shouldDocumentation = !(project.hasProperty('restDocs') && (project.property('restDocs') == 'no'))
}


test {
    outputs.dir snippetsDir
    useJUnitPlatform {}
}

task makeadoc {
    group 'documentation'
    dependsOn test
    onlyIf {shouldDocumentation}

    doLast {
        if (snippetsDir.exists()) {
            snippetsDir.eachDir {classDir ->
                file(classDir.canonicalPath).eachDir { methodDir ->
                    copy {
                        from('src/docs/template/') {
                            include('specification.adoc')
                        }
                        rename 'specification.adoc', methodDir.name + '.adoc'
                        into(Paths.get(snippetsDir.canonicalPath, classDir.name))

                        def fileList = file(methodDir).listFiles().collect{f -> return f.name}
                        def isPathParametersExists = fileList.contains("path-parameters.adoc")
                        def isRequestFieldsExists = fileList.contains("request-fields.adoc")
                        def isRequestParametersExists = fileList.contains("request-parameters.adoc")
                        def expandMap = [:]
                        expandMap.put("className", classDir.name)
                        expandMap.put("methodName", methodDir.name)
                        expandMap.put("isPathParametersExists", isPathParametersExists.toString())
                        expandMap.put("isRequestFieldsExists", isRequestFieldsExists.toString())
                        expandMap.put("isRequestParametersExists", isRequestParametersExists.toString())
                        expand(expandMap)
                    }
                }
            }
        }
    }
}

asciidoctor {
    group 'documentation'
    dependsOn makeadoc

    onlyIf {shouldDocumentation}

    inputs.dir snippetsDir
    attributes 'snippets': snippetsDir
}


bootJar {
    dependsOn asciidoctor // (3)
    from ("${asciidoctor.outputDir}/html5") { // (4)
        into 'static/docs'
    }
}
